//root

subprojects {
    apply plugin: 'base'

    task dist(type: Zip) {
        //println "zipping source/${task.project.name}/"
        from("." ) {
            exclude 'build*'
            exclude 'target'
            exclude '**/.gitkeep'
        }
        archiveName "${project.name}.zip"
    }

    configurations {
        distConfig
        compileSource 
    }

    artifacts {
        //compileSource dist
        distConfig dist
    }
    
    task unzipPack(type: Copy, dependsOn: dist) {
        def zipFile = project.file("build/distributions/${project.name}.zip")
        from zipTree(zipFile)
        into 'target/zipSource'
    }

    task copyPack(type: Copy, dependsOn: dist) {
        def zipFile = project.file("build/distributions/${project.name}.zip")
        from zipFile
        into 'target'
    }

    task copyDeps(type: Copy, dependsOn: dist) {
        from configurations.compileSource
        into project.file('target/zipSource/dependencies')
    }

    task unzipDeps(dependsOn: copyDeps) {
        doLast {
            fileTree(dir: project.file('target/zipSource/dependencies'))
                .include('*.zip')
                .each { depZipFile ->
                    copy {
                        //println "Copying ${depZipFile}"
                        def depName = depZipFile.name - '.zip'
                        from zipTree(depZipFile)
                        into project.file("target/zipSource/dependencies/${depName}")
                    }
                    delete depZipFile
            }
        }
    }

    task zipAll(type: Zip, dependsOn: [unzipDeps, unzipPack]) {
        destinationDir project.file('target')
        archiveName "${project.name}-all.zip"
        from(project.file('target/zipSource')) 
        //into(project.file('target'))
    }

    task deleteZipSource(type: Delete, dependsOn: [copyPack, zipAll]) {
        delete project.file('target/zipSource/')
    }


    task doAll(dependsOn: [copyDeps, unzipPack, copyPack, unzipDeps, zipAll, deleteZipSource])

    task printDeps() {
        doLast {
            configurations.compileSource.each { dep -> 
                println "compileSource $dep"
            }
        }
    }

    build {
        dependsOn copyDeps
        dependsOn unzipPack
        dependsOn copyPack
        dependsOn unzipDeps
        dependsOn zipAll
        dependsOn deleteZipSource
    }

}

    
